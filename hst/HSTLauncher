; HST Launcher
; ------------
;
; Date: 2017-12-19
; Author: Henrik Noerfjand Stengaard
;
; An AmigaDOS script to show a menu for launching games or demos.


; Clear screen
echo "*ec" NOLINE

; Error, if HSTLauncher: assign doesn't exist
Assign >NIL: EXISTS HSTLauncher:
IF WARN
  REQUESTCHOICE "Error" "HST Launcher assign HSTLauncher: doesn't exist!" "OK" >NIL:
  SKIP end
ENDIF

; Error, if menu file doesn't exist
IF NOT EXISTS "HSTLauncher:menu.lst"
  REQUESTCHOICE "Error" "HST Launcher menu file 'HSTLauncher:menu.lst'*Ndoesn't exist!" "OK" >NIL:
  SKIP end
ENDIF

; Start music
IF EXISTS "ENV:hstlaunchermusic"
  IF EXISTS "S:HSTLauncherMusic"
    echo "`execute S:HSTLauncherMusic START`"
  ENDIF
ENDIF


; main menu
; ---------
LAB mainmenu

; Build main menu
echo "Find" >"T:_hstlaunchermainmenu"
echo "Settings" >>"T:_hstlaunchermainmenu"
echo "Quit" >>"T:_hstlaunchermainmenu"
echo "----------------------------------------" >>"T:_hstlaunchermainmenu"
type "HSTLauncher:menu.lst" >>"T:_hstlaunchermainmenu"

; Show main menu
set mainmenuindex ""
set mainmenuindex "`RequestList TITLE="HST Launcher" LISTFILE="T:_hstlaunchermainmenu" WIDTH=640 LINES=24`"
Delete >NIL: "T:_hstlaunchermainmenu"

; Goto end, if request list was closed
IF NOT "$mainmenuindex" GT 0 VAL
  SKIP end
ENDIF

; Goto find
IF "$mainmenuindex" EQ 1 VAL
  SKIP find
ENDIF

; Goto settings
IF "$mainmenuindex" EQ 2 VAL
  SKIP settings
ENDIF

; Goto end
IF "$mainmenuindex" EQ 3 VAL
  SKIP end
ENDIF

; Main main, if item is less than 5
IF NOT "$mainmenuindex" GT 4 VAL
  SKIP BACK mainmenu
ENDIF

; Adjust main menu index
set mainmenuindex `eval $mainmenuindex - 4` 

; Get main menu item from menu file
echo "$mainmenuindex" >T:_mainmenuindex
set mainmenulineregexp `sed "s/\(.\)*$/\1q;d/" T:_mainmenuindex`
set mainmenuitem "`sed "$mainmenulineregexp" "HSTLauncher:menu.lst"`"
delete >NIL: T:_mainmenuindex

; Goto main menu, if sub menu file doesn't exist
IF NOT EXISTS "HSTLauncher:$mainmenuindex/menu.lst"
  REQUESTCHOICE "Error" "HST Launcher sub menu file 'HSTLauncher:$mainmenuindex/menu.lst'*Ndoesn't exist!" "OK" >NIL:
  SKIP BACK mainmenu
ENDIF


; sub menu
; --------
LAB submenu

; Build sub main
echo "Back" >"T:_hstlaunchersubmenu"
echo "----------------------------------------" >>"T:_hstlaunchersubmenu"
type "HSTLauncher:$mainmenuindex/menu.lst" >>"T:_hstlaunchersubmenu"

; Show sub menu
set submenuindex ""
set submenuindex "`RequestList TITLE="$mainmenuitem" LISTFILE="T:_hstlaunchersubmenu" WIDTH=640 LINES=24`"
Delete >NIL: "T:_hstlaunchersubmenu"

; Goto main menu, if item is less than 2
IF NOT "$submenuindex" GT 1 VAL
  SKIP BACK mainmenu
ENDIF

; Goto main menu, if item is less than 3
IF NOT "$submenuindex" GT 2 VAL
  SKIP BACK submenu
ENDIF

; Adjust sub menu index
set submenuindex `eval $submenuindex - 2` 

; Goto run, if sub menu index run file exists
IF EXISTS "HSTLauncher:$mainmenuindex/$submenuindex.run"
  set returnlab "submenu"
  set runfile "$mainmenuindex/$submenuindex.run"
  SKIP run
ENDIF

SKIP BACK submenu


; Find
; ----
LAB find

; Error, if search file doesn't exist
IF NOT EXISTS "HSTLauncher:search.lst"
  REQUESTCHOICE "Error" "HST Launcher search file 'HSTLauncher:search.lst'*Ndoesn't exist!" "OK" >NIL:
  SKIP BACK mainmenu
ENDIF

; Enter search text
Delete >NIL: T:_result#?
set searchtext `RequestString "Find" "Enter search text"` 

; Find search text in search file and limit to first 25 results
search >T:_result "HSTLauncher:search.lst" "$searchtext" NONUM
head >T:_resultlimited -25 T:_result
cut >T:_resultnames1 -f 1 T:_resultlimited

; Build find results menu
echo "Find" >T:_resultnames2
echo "Back" >>T:_resultnames2
echo "----------------------------------------" >>T:_resultnames2
sed >>T:_resultnames2 "1d" T:_resultnames1
cut -f 2 T:_resultlimited >>T:_resultrunfiles1
echo "" >T:_resultrunfiles2
echo "" >>T:_resultrunfiles2
echo "" >>T:_resultrunfiles2
sed >>T:_resultrunfiles2 "1d" T:_resultrunfiles1


; Result
; ------
LAB result

; Show result menu
set resultindex ""
set resultindex "`RequestList TITLE="Find '$searchtext' results" LISTFILE="T:_resultnames2" WIDTH=640 LINES=24`"

; Goto main menu, if index is less than 1
IF NOT "$resultindex" GT 0 VAL
  Delete >NIL: T:_result#?
  SKIP BACK mainmenu
ENDIF

; Goto find, if index is 1
IF "$resultindex" EQ 1 VAL
  SKIP BACK find
ENDIF

; Goto main menu, if index is 2
IF "$resultindex" EQ 2 VAL
  Delete >NIL: T:_result#?
  SKIP BACK mainmenu
ENDIF

; Goto result, if index is less than 4
IF NOT "$resultindex" GT 3 VAL
  SKIP BACK result
ENDIF

; Get run file from result index
echo "$resultindex" >T:_resultindex
set resultindexregexp `sed "s/\(.\)*$/\1q;d/" T:_resultindex`
set runfile "`sed "$resultindexregexp" "T:_resultrunfiles2"`"
delete >NIL: T:_resultindex

; Goto run, if result run file exists
IF EXISTS "HSTLauncher:$runfile"
  set returnlab "result"
  SKIP run
ENDIF

SKIP BACK result


; Settings
; --------
LAB settings

; Goto main menu, if settings menu file doesn't exist
IF NOT EXISTS "HSTLauncher:.Settings/menu.lst"
  REQUESTCHOICE "Error" "HST Launcher settings menu file 'HSTLauncher:.Settings/menu.lst'*Ndoesn't exist!" "OK" >NIL:
  SKIP BACK mainmenu
ENDIF

; Build settings menu
echo "Back" >T:_hstlaunchersettings
echo "----------------------------------------" >>T:_hstlaunchersettings
type "HSTLauncher:.Settings/menu.lst" >>"T:_hstlaunchersettings"

; Show settings menu
set settingsindex ""
set settingsindex "`RequestList TITLE="Settings" LISTFILE="T:_hstlaunchersettings" WIDTH=640 LINES=24`"
delete >NIL: T:_hstlaunchersettings

; Goto main menu, if index is less than 2
IF NOT "$settingsindex" GT 1 VAL
  SKIP BACK mainmenu
ENDIF

; Goto settings, if index is less than 3
IF NOT "$settingsindex" GT 2 VAL
  SKIP BACK settings
ENDIF

; Adjust sub menu index
set settingsindex `eval $settingsindex - 2` 

; Goto run, if settings run file exists
IF EXISTS "HSTLauncher:.Settings/$settingsindex.run"
  echo "*ec" NOLINE
  echo "`execute "HSTLauncher:.Settings/$settingsindex.run"`"
  wait 1
  echo "*ec" NOLINE
ENDIF

SKIP BACK settings


; Run
; ---
LAB run

; Stop music
IF EXISTS "ENV:hstlaunchermusic"
  IF EXISTS "S:HSTLauncherMusic"
    echo "`execute S:HSTLauncherMusic STOP`"
  ENDIF
ENDIF

; Start run file, if it exists
echo "Starting 'HSTLauncher:$runfile'..."
IF EXISTS "HSTLauncher:$runfile"
  echo "*ec" NOLINE
  avail >NIL: flush
  echo "`execute "HSTLauncher:$runfile"`"
  wait 1
  echo "*ec" NOLINE
ENDIF

; Start music
IF EXISTS "ENV:hstlaunchermusic"
  IF EXISTS "S:HSTLauncherMusic"
    echo "`execute S:HSTLauncherMusic START`"
  ENDIF
ENDIF

SKIP BACK $returnlab


; End
; ---
LAB end

; Stop music
IF EXISTS "S:HSTLauncherMusic"
  echo "`execute S:HSTLauncherMusic STOP`"
ENDIF