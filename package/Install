; EAB WHDLoad Games OCS Menu Package Install Script
; -------------------------------------------------
;
; Author: Henrik Noerfjand Stengaard
; Date: 2019-08-08
;
; This script extracts and installs zip archives for EAB WHDLoad Games OCS Menu Package.


; Reset eab menu
SET eabmenuags2="Yes"
SET eabmenuhst="Yes"
SET eabmenuigame="Yes"
SET eabmenuwhdload="Yes"
SET eabmenuigameset="320x128"


; Build eab menu
LAB eabmenu

echo "" NOLINE >"T:_eabmenu"
echo "Install AGS2 Menu:               $eabmenuags2" >>"T:_eabmenu"
echo "Install HST Launcher Menu:       $eabmenuhst" >>"T:_eabmenu"
echo "Install iGame Screenshots:       $eabmenuigame" >>"T:_eabmenu"
echo "Configure WHDLoad.prefs:         $eabmenuwhdload" >>"T:_eabmenu"
echo "--------------------------------------------" >>"T:_eabmenu"
echo "Selected iGame Screenshot Set:   $eabmenuigameset" >>"T:_eabmenu"
echo "--------------------------------------------" >>"T:_eabmenu"
echo "Start Installation" >>"T:_eabmenu"


; Show eab menu
set eabmenu "`RequestList TITLE="EAB WHDLoad Games OCS Menu Package" LISTFILE="T:_eabmenu" WIDTH=480 LINES=16`"
delete "T:_eabmenu" >NIL:

; Install AGS2 Menu option
IF "$eabmenu" EQ 1 VAL
  IF "$eabmenuags2" EQ "Yes"
    SET eabmenuags2="No"
  ELSE
    SET eabmenuags2="Yes"
  ENDIF
ENDIF

; Install HST Menu option
IF "$eabmenu" EQ 2 VAL
  IF "$eabmenuhst" EQ "Yes"
    SET eabmenuhst="No"
  ELSE
    SET eabmenuhst="Yes"
  ENDIF
ENDIF

; Install iGame Screenshots option
IF "$eabmenu" EQ 3 VAL
  IF "$eabmenuigame" EQ "Yes"
    SET eabmenuigame="No"
  ELSE
    SET eabmenuigame="Yes"
  ENDIF
ENDIF

; Configure WHDLoad.prefs option
IF "$eabmenu" EQ 4 VAL
  IF "$eabmenuwhdload" EQ "Yes"
    SET eabmenuwhdload="No"
  ELSE
    SET eabmenuwhdload="Yes"
  ENDIF
ENDIF

; Selected iGame Screenshot Set option
IF "$eabmenu" EQ 6 VAL
  IF "$eabmenuigameset" EQ "320x128"
    SET eabmenuigameset="320x256"
  ELSE
    SET eabmenuigameset="320x128"
  ENDIF
ENDIF

; Start Installation option
IF "$eabmenu" EQ 8 VAL
  SKIP install
ENDIF

SKIP BACK eabmenu


; Install
; -------

LAB install

; Copy readme and screenshots to locale help for EAB WHDLoad Games OCS Menu
echo "Installing Readme..."
IF NOT EXISTS SYSTEMDIR:Locale/Help/EAB-WHDLoad-Games-OCS-Menu
  MakePath >NIL: SYSTEMDIR:Locale/Help/EAB-WHDLoad-Games-OCS-Menu
  IF EXISTS SYSTEMDIR:Prefs/Env-Archive/Sys/def_drawer.info
    makeicon >NIL: SYSTEMDIR:Locale/Help/EAB-WHDLoad-Games-OCS-Menu FROM SYSTEMDIR:Prefs/Env-Archive/Sys/def_drawer.info 
  ELSE
    makeicon >NIL: SYSTEMDIR:Locale/Help/EAB-WHDLoad-Games-OCS-Menu
  ENDIF
ENDIF
IF NOT EXISTS SYSTEMDIR:Locale/Help.info
  IF EXISTS SYSTEMDIR:Prefs/Env-Archive/Sys/def_drawer.info
    makeicon >NIL: SYSTEMDIR:Locale/Help FROM SYSTEMDIR:Prefs/Env-Archive/Sys/def_drawer.info 
  ELSE
    makeicon >NIL: SYSTEMDIR:Locale/Help
  ENDIF
ENDIF
IF NOT EXISTS SYSTEMDIR:Locale.info
  IF EXISTS SYSTEMDIR:Prefs/Env-Archive/Sys/def_drawer.info
    makeicon >NIL: SYSTEMDIR:Locale FROM SYSTEMDIR:Prefs/Env-Archive/Sys/def_drawer.info 
  ELSE
    makeicon >NIL: SYSTEMDIR:Locale
  ENDIF
ENDIF
copy >NIL: PACKAGEDIR:README SYSTEMDIR:Locale/Help/EAB-WHDLoad-Games-OCS-Menu
copy >NIL: PACKAGEDIR:README.guide SYSTEMDIR:Locale/Help/EAB-WHDLoad-Games-OCS-Menu
copy >NIL: PACKAGEDIR:README.info SYSTEMDIR:Locale/Help/EAB-WHDLoad-Games-OCS-Menu
IF NOT EXISTS SYSTEMDIR:Locale/Help/EAB-WHDLoad-Games-OCS-Menu/screenshots
  MakePath >NIL: SYSTEMDIR:Locale/Help/EAB-WHDLoad-Games-OCS-Menu/screenshots
ENDIF
copy >NIL: PACKAGEDIR:screenshots/#?.iff SYSTEMDIR:Locale/Help/EAB-WHDLoad-Games-OCS-Menu/screenshots


; Install system files
; --------------------

echo "Installing System Files..."
unzip -qq -o -x PACKAGEDIR:system-files.zip -d SYSTEMDIR:


; Configure WHDLoad arguments
; ---------------------------

; Create systemdir whdload prefs directory, if it doesn't exist
IF NOT EXISTS SYSTEMDIR:Prefs/WHDLoad
  MakePath >NIL: SYSTEMDIR:Prefs/WHDLoad
ENDIF

; Create default whdloadargs, if it doesn't exist
IF NOT EXISTS SYSTEMDIR:Prefs/WHDLoad/whdloadargs
  echo "Preload" >SYSTEMDIR:Prefs/WHDLoad/whdloadargs
ENDIF


; Install Frontends Games
; -----------------------

echo "Installing Frontends Games..."
unzip -qq -o -x PACKAGEDIR:frontends-games.zip -d FRONTENDSDIR:

echo "Installing Frontends Games All..."
unzip -qq -o -x PACKAGEDIR:frontends-games-all.zip -d FRONTENDSDIR:


; Configure icons
; ---------------

; Install games icons
unzip -qq -o -x PACKAGEDIR:games-icons.zip -d GAMESDIR:

; Add icon for programs drawer, if it doesn't exist
IF NOT EXISTS SYSTEMDIR:Programs.info
  rename >NIL: SYSTEMDIR:Programs.info.new SYSTEMDIR:Programs.info
ELSE
  delete >NIL: SYSTEMDIR:Programs.info.new
ENDIF

; Add icon for configuration drawer, if it doesn't exist
IF NOT EXISTS SYSTEMDIR:Programs/Configuration.info
  rename >NIL: SYSTEMDIR:Programs/Configuration.info.new SYSTEMDIR:Programs/Configuration.info
ELSE
  delete >NIL: SYSTEMDIR:Programs/Configuration.info.new
ENDIF


; Configure WHDLoad.prefs
; -----------------------

; Skip to install AGS2, if configure whdload.prefs is set to no
IF NOT "$eabmenuwhdload" EQ "Yes"
  SKIP installags2
ENDIF

echo "Configuring WHDLoad.prefs..."
IF NOT EXISTS SYSTEMDIR:S
  MakePath >NIL: SYSTEMDIR:S
ENDIF
IF NOT EXISTS "SYSTEMDIR:S/WHDLoad.prefs"
  Copy >NIL: PACKAGEDIR:WHDLoad.prefs SYSTEMDIR:S
ENDIF

; Create whdload saves directory
IF NOT EXISTS "$SAVESDIR"
  MakePath >NIL: "$SAVESDIR"
ENDIF

; Disable existing save path in WHDLoad.prefs
rep "SYSTEMDIR:S/WHDLoad.prefs" "SavePath" ";SavePath"

; Set save path in WHDLoad.prefs
echo "SavePath=$SAVESDIR" >>SYSTEMDIR:S/WHDLoad.prefs

; Disable existing splash deplay
rep "SYSTEMDIR:S/WHDLoad.prefs" "SplashDelay" ";SplashDelay"

; Set splash deplay in WHDLoad.prefs
echo "SplashDelay=50" >>SYSTEMDIR:S/WHDLoad.prefs


; Install AGS2
; ------------

LAB installags2

; Skip to install hst, if install ags2 is set to no
IF NOT "$eabmenuags2" EQ "Yes"
  SKIP installhstlauncher
ENDIF

echo "Installing AGS2 Games Menu..."
set ags2gamesmenudir "`execute INSTALLDIR:S/CombinePath "$MENUSDIR" "AGS2Games"`"

IF NOT EXISTS "$ags2gamesmenudir"
  MakePath "$ags2gamesmenudir" >NIL:
ENDIF
unzip -qq -o -x PACKAGEDIR:ags2.zip -d "$ags2gamesmenudir"
IF NOT EXISTS "$ags2gamesmenudir/Music"
  MakePath "$ags2gamesmenudir/Music" >NIL:
ENDIF
IF NOT EXISTS "$ags2gamesmenudir/.Favourites.ags"
  IF NOT EXISTS "$ags2gamesmenudir/.Favourites.ag_"
    MakePath >NIL: "$ags2gamesmenudir/.Favourites.ag_"
  ENDIF
ENDIF

echo "Installing AGS2 Games All Menu..."
unzip -qq -o -x PACKAGEDIR:ags2-games-all.zip -d "$ags2gamesmenudir"

echo "Configuring AGS2 Games Menu..."

; Create systemdir ags2 prefs directory, if it doesn't exist
IF NOT EXISTS SYSTEMDIR:Prefs/AGS2
  MakePath >NIL: SYSTEMDIR:Prefs/AGS2
ENDIF

; Create ags2music prefs to turn on music in ags2 menu
IF NOT EXISTS SYSTEMDIR:Prefs/AGS2/ags2music
  echo "" NOLINE > SYSTEMDIR:Prefs/AGS2/ags2music
ENDIF

; Add or update AGS2Menus.cfg
search SYSTEMDIR:Prefs/AGS2/AGS2Menus.cfg "AGS2Games=" >NIL:
IF WARN
  echo "AGS2Games=$ags2gamesmenudir" >>SYSTEMDIR:Prefs/AGS2/AGS2Menus.cfg
ELSE
  rep SYSTEMDIR:Prefs/AGS2/AGS2Menus.cfg "AGS2Games=" "AGS2Games=$ags2gamesmenudir*N; "
ENDIF


; Install HST Launcher
; --------------------

LAB installhstlauncher

; Skip to install igame, if install hst is set to no
IF NOT "$eabmenuhst" EQ "Yes"
  SKIP installigame
ENDIF

echo "Installing HST Launcher Games Menu..."

set hstgamesmenudir "`execute INSTALLDIR:S/CombinePath "$MENUSDIR" "HSTGames"`"
IF NOT EXISTS "$hstgamesmenudir"
  MakePath "$hstgamesmenudir" >NIL:
ENDIF
unzip -qq -o -x PACKAGEDIR:hst.zip -d "$hstgamesmenudir"
IF NOT EXISTS "$hstgamesmenudir/Music"
  MakePath "$hstgamesmenudir/Music" >NIL:
ENDIF

set hstgames1mbmenudir "`execute INSTALLDIR:S/CombinePath "$MENUSDIR" "HSTGames1MB"`"
IF NOT EXISTS "$hstgames1mbmenudir"
  MakePath "$hstgames1mbmenudir" >NIL:
ENDIF
unzip -qq -o -x PACKAGEDIR:hst.zip -d "$hstgames1mbmenudir"
IF NOT EXISTS "$hstgames1mbmenudir/Music"
  MakePath "$hstgames1mbmenudir/Music" >NIL:
ENDIF

echo "Installing HST Games All Menu..."
unzip -qq -o -x PACKAGEDIR:hst-games-all.zip -d "$hstgamesmenudir"
echo "Installing HST Games 1MB Menu..."
unzip -qq -o -x PACKAGEDIR:hst-games-1mb.zip -d "$hstgames1mbmenudir"

echo "Configuring HST Launcher Games Menu..."

; Create systemdir hst launcher prefs directory, if it doesn't exist
IF NOT EXISTS SYSTEMDIR:Prefs/HSTLauncher
  MakePath >NIL: SYSTEMDIR:Prefs/HSTLauncher
ENDIF

; Add or update HSTLauncherMenus.cfg
search SYSTEMDIR:Prefs/HSTLauncher/HSTLauncherMenus.cfg "HSTGames=" >NIL:
IF WARN
  echo "HSTGames=$hstgamesmenudir" >>SYSTEMDIR:Prefs/HSTLauncher/HSTLauncherMenus.cfg
ELSE
  rep SYSTEMDIR:Prefs/HSTLauncher/HSTLauncherMenus.cfg "HSTGames=" "HSTGames=$hstgamesmenudir*N; "
ENDIF
search SYSTEMDIR:Prefs/HSTLauncher/HSTLauncherMenus.cfg "HSTGames1MB=" >NIL:
IF WARN
  echo "HSTGames1MB=$hstgames1mbmenudir" >>SYSTEMDIR:Prefs/HSTLauncher/HSTLauncherMenus.cfg
ELSE
  rep SYSTEMDIR:Prefs/HSTLauncher/HSTLauncherMenus.cfg "HSTGames1MB=" "HSTGames1MB=$hstgames1mbmenudir*N; "
ENDIF


; Install iGame
; -------------

LAB installigame

; Skip to configure assigns, if install igame is set to no
IF NOT "$eabmenuigame" EQ "Yes"
  SKIP configureassigns
ENDIF

; Install iGame Screenshot Menu
IF "$eabmenuigameset" EQ "320x128"
  echo "Installing iGame Games All 320x128 Screenshots..."
  unzip -qq -o -x PACKAGEDIR:igame-320x128-all.zip -d GAMESDIR:
ELSE
  echo "Installing iGame Games All 320x256 Screenshots..."
  unzip -qq -o -x PACKAGEDIR:igame-320x256-all.zip -d GAMESDIR:
ENDIF

echo "Configuring iGame Games Menu..."

; Create systemdir igame prefs directory, if it doesn't exist
IF NOT EXISTS SYSTEMDIR:Prefs/iGame
  MakePath >NIL: SYSTEMDIR:Prefs/iGame
ENDIF

; Add or update iGameMenus.cfg
search SYSTEMDIR:Prefs/iGame/iGameMenus.cfg "iGameGames=" >NIL:
IF WARN
  echo "iGameGames=$FRONTENDSDIR" >>SYSTEMDIR:Prefs/iGame/iGameMenus.cfg
ELSE
  rep SYSTEMDIR:Prefs/iGame/iGameMenus.cfg "iGameGames=" "iGameGames=$FRONTENDSDIR*N; "
ENDIF


; Configure assigns
; -----------------

LAB configureassigns

echo "Configuring Assigns..."

; Create assign startup, if it doesn't exist
IF NOT EXISTS SYSTEMDIR:S/Assign-Startup
	echo "" NOLINE >SYSTEMDIR:S/Assign-Startup
ENDIF

; Add execute assign startup to startup sequence, if it not ClassicWB and doesn't contain execute assign startup
search SYSTEMDIR:S/Startup-Sequence "ClassicWB" >NIL:
IF WARN
  search SYSTEMDIR:S/Startup-Sequence "execute S:Assign-Startup" >NIL:
  IF WARN
    rep SYSTEMDIR:S/Startup-Sequence "BindDrivers" "IF EXISTS S:Assign-Startup*N  execute S:Assign-Startup*NENDIF*N*NBindDrivers"
  ENDIF
ENDIF

; Add or update A-Games assign
search SYSTEMDIR:S/Assign-Startup "A-Games:" >NIL:
IF WARN 
	echo "Assign >NIL: A-Games: $GAMESDIR" >>SYSTEMDIR:S/Assign-Startup
ELSE
  setenv updateassign `RequestChoice "Update assign" "A-Games: assign already exists and must be updated to*Npath '$GAMESDIR'.*N*NNote: Updating assign requires a reboot after*Ninstallation is done!*N*NDo you want to update A-Games: assign?" "Yes|No"`
  IF "$updateassign" EQ "1"
    echo "$GAMESDIR" >T:_gamesdir
    set assigngamesdir "`sed "s/\//\\\//g" T:_gamesdir`"
    sed "s/\([Aa]-[Gg][Aa][Mm][Ee][Ss]:\)/\1 $assigngamesdir;/" "SYSTEMDIR:S/Assign-Startup" >"T:Assign-Startup"
    copy >NIL: "T:Assign-Startup" "SYSTEMDIR:S/Assign-Startup"
  ENDIF   
ENDIF
