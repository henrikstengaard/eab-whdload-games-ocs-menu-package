.KEY ags2menu/a
.BRA {
.KET }

; AGS2 Menu
; ---------
;
; Date: 2017-12-19
; Author: Henrik Noerfjand Stengaard
;
; An AmigaDOS script to start AGS2 menus.


; Fail, if AGS2Menus.cfg file doesn't exist
IF NOT EXISTS "SYS:Prefs/AGS2/AGS2Menus.cfg"
  REQUESTCHOICE "Error" "AGS2 menu configuration file*N'SYS:Prefs/AGS2/AGS2Menus.cfg'*Ndoesn't exist!" "OK" >NIL:
  SKIP end
ENDIF

; Fail, if AGS2Menus.cfg file doesn't contain ags2 menu
search "SYS:Prefs/AGS2/AGS2Menus.cfg" "{ags2menu}=" NONUM >T:_ags2dir
IF WARN
  REQUESTCHOICE "Error" "AGS2 menu configuration file*N'SYS:Prefs/AGS2/AGS2Menus.cfg'*Ndoesn't contain '{ags2menu}'!" "OK" >NIL:
  SKIP end
ENDIF


; Execute remove AGS2 favourites, if it exists
IF EXISTS S:RemoveAgs2Favourites
	execute S:RemoveAgs2Favourites
	delete >NIL: S:RemoveAgs2Favourites
ENDIF 

; Load whdloadargs to env, if it exists
IF EXISTS SYS:Prefs/WHDLoad/whdloadargs
  copy >NIL: SYS:Prefs/WHDLoad/whdloadargs ENV:whdloadargs
ENDIF

; Load ags2favouritesmode to env, if it exists
IF EXISTS SYS:Prefs/AGS2/ags2favouritesmode
  copy >NIL: SYS:Prefs/AGS2/ags2favouritesmode ENV:ags2favouritesmode
ENDIF

; Load ags2music to env, if it exists
IF EXISTS SYS:Prefs/AGS2/ags2music
  copy >NIL: SYS:Prefs/AGS2/ags2music ENV:ags2music
ENDIF


; Set ags2 dir from ags2 menu
setenv ags2dir `sed "s/^{ags2menu}=//" T:_ags2dir`
delete "T:_ags2dir" >NIL:

; Assign AGS
Assign AGS: "$ags2dir"

; Start AGS2, if it exists
IF EXISTS AGS:AGS2
  ; AGS2 Music Start
  IF EXISTS "ENV:ags2music"
    IF EXISTS S:AGS2Music
      execute S:AGS2Music START
    ENDIF
  ENDIF

  ; Start AGS2
  AGS:AGS2

  ; AGS2 Music Stop
  IF EXISTS "ENV:ags2music"
    IF EXISTS S:AGS2Music
      execute S:AGS2Music STOP
    ENDIF
  ENDIF
ENDIF

; Remove AGS assign
ASSIGN AGS: "$ags2dir" REMOVE

; End
LAB end